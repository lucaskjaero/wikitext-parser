plugins {
    id "com.diffplug.spotless" version "6.25.0"
    id "io.freefair.lombok" version "8.10"
    id "jacoco"
    id "java-library"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.squareup.retrofit2:retrofit:2.11.0"
    api "com.squareup.retrofit2:converter-gson:2.11.0"
    api 'com.google.code.gson:gson:2.11.0'
    testImplementation "org.junit.jupiter:junit-jupiter:5.11.1"
    testImplementation "org.mockito:mockito-core:5.14.0"
}

/*
 * Formatter configuration
 */

spotless {
    antlr4 {
        target "src/*/antlr/**/*.g4"
        antlr4Formatter()
    }

    // Needed to exclude antlr generated code.
    // Have to edit the file tree because of https://github.com/diffplug/spotless/issues/1163
    // This causes task dependency to not be automatically inferred, so we need to declare dependencies.
    java {
        target project.fileTree(project.rootDir) {
            include "common/**/*.java"
            exclude "common/build/generated-src/**/*.*"
        }
        removeUnusedImports()
        googleJavaFormat()
    }

    json {
        target "src/**/*.json"
        simple().indentWithSpaces(4)
    }
}
// Manually register dependencies because we edit the project file tree
spotlessJava.dependsOn(compileJava, compileTestJava, jacocoTestReport, test, spotlessAntlr4, spotlessJson)

/*
 * Test configuration
 */

tasks.named("test") {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

check.dependsOn jacocoTestReport