plugins {
    id "antlr"
    id "com.diffplug.spotless" version "6.25.0"
    id "io.freefair.lombok" version "8.10"
    id "jacoco"
    id "java-library"
}

repositories {
    mavenCentral()
}

dependencies {
    // api dependencies are exported to consumers
    // implementation dependencies are not.
    api project(':common')
    api project(':preprocessor')

    antlr "org.antlr:antlr4:4.13.2"
    api "org.antlr:antlr4-runtime:4.13.2"

    testImplementation "org.junit.jupiter:junit-jupiter:5.11.1"
}


/*
 * Antlr configuration
 */

generateGrammarSource {
    arguments += [
        // Generate state transition graphs
        "-atn",
        // Needed to make gradle aware of grammar imports.
        "-lib", "src/main/antlr/com/lucaskjaerozhang/wikitext_parser/grammar/parse",
        "-package", "com.lucaskjaerozhang.wikitext_parser.grammar.parse",
        "-long-messages",
        "-visitor",
    ]
}

/*
 * Formatter configuration
 */

spotless {
    antlr4 {
        target "src/*/antlr/**/*.g4"
        antlr4Formatter()
    }

    java {
        // Needed to exclude antlr generated code.
        // Have to edit the file tree because of https://github.com/diffplug/spotless/issues/1163
        // This causes task dependency to not be automatically inferred, so we need to declare dependencies.
        target project.fileTree(project.rootDir) {
            include "lib/**/*.java"
            exclude "lib/build/generated-src/**/*.*"
        }
        removeUnusedImports()
        googleJavaFormat()
    }

    json {
        target "src/**/*.json"
        simple().indentWithSpaces(4)
    }
}
// Manually register dependencies because we edit the project file tree
spotlessJava.dependsOn(compileJava, compileTestJava, jacocoTestReport, test, spotlessAntlr4, spotlessJson)

// Workaround for https://github.com/gradle/gradle/issues/19555
sourceSets.configureEach {
    java.srcDir(tasks.named(getTaskName("generate", "GrammarSource")).map { files() })
}

/*
 * Test configuration
 */

tasks.named("test") {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

check.dependsOn jacocoTestReport

/*
 * Githooks
 * Makes sure formatter is run and javadocs are generated for every commit.
 */

// Automatically run formatter on git commit.
tasks.register("installGitHooks", Copy) {
    from "./../scripts/pre-commit"
    into "./../.git/hooks"
}
compileJava.dependsOn installGitHooks

/*
 * Javadocs
 */

javadoc {
    source = sourceSets.main.allJava

    // This is the path for antlr generated code, which we should not include.
    exclude "com/lucaskjaerozhang/wikitext_parser/grammar/**"

    options.tags = [
        "horizontalRule",
    ]
}

task docs(type: Copy) {
    from "build/docs/javadoc"
    into "../docs"
}
docs.dependsOn javadoc