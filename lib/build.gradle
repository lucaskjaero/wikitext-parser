plugins {
    // Apply the java-library plugin for API and implementation separation.
    id 'java-library'
    id 'antlr'
    id "com.diffplug.spotless" version "6.9.0"
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    // Use JUnit Jupiter for testing.
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'

    // This dependency is exported to consumers, that is to say found on their compile classpath.
    api 'org.apache.commons:commons-math3:3.6.1'

    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    implementation 'com.google.guava:guava:31.0.1-jre'

    antlr "org.antlr:antlr4:4.10.1"
    api "org.antlr:antlr4-runtime:4.10.1"
}

generateGrammarSource {
    arguments += ["-package", "com.lucaskjaerozhang.wikitext_parser.grammar", "-long-messages", "-visitor"]
}

tasks.named('test') {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
}

spotless {
    java {
        target project.fileTree(project.rootDir) {
            include '**/*.java'
            exclude 'lib/build/generated-src/**/*.*'
        }
        removeUnusedImports()
        googleJavaFormat()
    }

    antlr4 {
        target 'src/*/antlr/**/*.g4'
        antlr4Formatter()
    }
}

