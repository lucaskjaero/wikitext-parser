plugins {
    id 'java-library'
    id 'antlr'
    id 'com.diffplug.spotless' version '6.9.1'
    id 'jacoco'
}

repositories {
    mavenCentral()
}

dependencies {
    // api dependencies are exported to consumers
    // implementation dependencies are not.

    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'

    antlr "org.antlr:antlr4:4.10.1"
    api "org.antlr:antlr4-runtime:4.10.1"
}


// ANTLR configuration
generateGrammarSource {
    arguments += [
        // Generate state transition graphs
        "-atn",
        // Needed to make gradle aware of grammar imports.
        "-lib", "src/main/antlr/com/lucaskjaerozhang/wikitext_parser/grammar",
        "-package", "com.lucaskjaerozhang.wikitext_parser.grammar",
        "-long-messages",
        "-visitor",
    ]
}

spotless {
    java {
        target project.fileTree(project.rootDir) {
            include '**/*.java'
            exclude 'lib/build/generated-src/**/*.*'
        }
        removeUnusedImports()
        googleJavaFormat()
    }

    antlr4 {
        target 'src/*/antlr/**/*.g4'
        antlr4Formatter()
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport

// Automatically run formatter on git commit.
tasks.register('installGitHooks', Copy) {
    from './../scripts/pre-commit'
    into './../.git/hooks'
}
compileJava.dependsOn installGitHooks

javadoc {
    source = sourceSets.main.allJava
    options.tags = [
        "horizontalRule",
    ]
}

task docs(type: Copy) {
    from 'build/docs/javadoc'
    into '../javadocs'
}
docs.dependsOn javadoc



// build.