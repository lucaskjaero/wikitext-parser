plugins {
    // Apply the java-library plugin for API and implementation separation.
    id 'java-library'
    id 'antlr'
    id "com.diffplug.spotless" version "6.9.0"
}

repositories {
    mavenCentral()
}

dependencies {
    // api dependencies are exported to consumers
    // implementation dependencies are not.

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'

    antlr "org.antlr:antlr4:4.10.1"
    api "org.antlr:antlr4-runtime:4.10.1"
}


// ANTLR configuration
generateGrammarSource {
    arguments += ["-package", "com.lucaskjaerozhang.wikitext_parser.grammar", "-long-messages", "-visitor"]
}

tasks.named('test') {
    useJUnitPlatform()
}

spotless {
    java {
        target project.fileTree(project.rootDir) {
            include '**/*.java'
            exclude 'lib/build/generated-src/**/*.*'
        }
        removeUnusedImports()
        googleJavaFormat()
    }

    antlr4 {
        target 'src/*/antlr/**/*.g4'
        antlr4Formatter()
    }
}

