plugins {
    id "antlr"
    id "com.diffplug.spotless" version "6.10.0"
    id "io.freefair.lombok" version "6.5.0.3"
    id "jacoco"
    id "java-library"
}

repositories {
    mavenCentral()
}

dependencies {
    // api dependencies are exported to consumers
    // implementation dependencies are not.

    testImplementation "org.junit.jupiter:junit-jupiter:5.9.0"

    antlr "org.antlr:antlr4:4.10.1"
    api "org.antlr:antlr4-runtime:4.10.1"
}


/*
 * Antlr configuration
 */

generateGrammarSource {
    arguments += [
        // Generate state transition graphs
        "-atn",
        // Needed to make gradle aware of grammar imports.
        "-lib", "src/main/antlr/com/lucaskjaerozhang/wikitext_parser/grammar/preprocess",
        "-package", "com.lucaskjaerozhang.wikitext_parser.grammar.preprocess",
        "-long-messages",
        "-visitor",
    ]
}

/*
 * Formatter configuration
 */

spotless {
    java {
        target project.fileTree(project.rootDir) {
            include "preprocessor/**/*.java"
            exclude "preprocessor/build/generated-src/**/*.*"
        }
        removeUnusedImports()
        googleJavaFormat()
    }

    antlr4 {
        target "src/*/antlr/**/*.g4"
        antlr4Formatter()
    }
}

/*
 * Test configuration
 */

tasks.named("test") {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport
